buildscript {
	repositories {
		jcenter()

		mavenCentral()

		maven {
			url "http://files.minecraftforge.net/maven"
		}
	}

	dependencies {
		classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.1"
	}
}

apply plugin: "net.minecraftforge.gradle"
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: "com.github.johnrengelman.shadow"

version = "1.15.2-1.21.1.2"
group = "com.therandomlabs.randompatches"
archivesBaseName = "randompatches"

// Need this here so eclipse task generates correctly.
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.targetCompatibility = JavaVersion.VERSION_1_8

ext {
    trlutilsClassesDir = file("build/trlutils")
}

configurations {
    nonModDep
}

minecraft {
	// The mappings can be changed at any time, and must be in the following format.
	// snapshot_YYYYMMDD   Snapshot are built nightly.
	// stable_#            Stables are built at the discretion of the MCP team.
	// Use non-default mappings at your own risk. they may not always work.
	// Simply re-run your setup task after changing the mappings to update your workspace.
	mappings channel: "snapshot", version: "20200126-1.15.1"

	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

	runs {
		client {
			environment "MOD_CLASSES", [
					sourceSets.main.output.resourcesDir.absolutePath,
					sourceSets.main.output.classesDirs.asPath,
					project.trlutilsClassesDir.absolutePath
			].join(File.pathSeparator)

			workingDirectory project.file("run")

			// Recommended logging data for a userdev environment
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			
			// Recommended logging level for the console
			property "forge.logging.console.level", "debug"

			mods {
				randompatches {
					source sourceSets.main
				}
			}
		}

		server {
			environment "MOD_CLASSES", [
					sourceSets.main.output.resourcesDir.absolutePath,
					sourceSets.main.output.classesDirs.asPath,
					project.trlutilsClassesDir.absolutePath
			].join(File.pathSeparator)

			workingDirectory project.file("run")

			// Recommended logging data for a userdev environment
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			
			// Recommended logging level for the console
			property "forge.logging.console.level", "debug"
			
			mods {
				randompatches {
					source sourceSets.main
				}
			}
		}

		data {
			environment "MOD_CLASSES", [
					sourceSets.main.output.resourcesDir.absolutePath,
					sourceSets.main.output.classesDirs.asPath,
					project.trlutilsClassesDir.absolutePath
			].join(File.pathSeparator)

			workingDirectory project.file('run')

			// Recommended logging data for a userdev environment
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			
			// Recommended logging level for the console
			property 'forge.logging.console.level', 'debug'

			args '--mod', 'randompatches', '--all', '--output', file('src/generated/resources/')

			mods {
				examplemod {
					source sourceSets.main
				}
			}
		}
	}
}

repositories {
	mavenCentral()

	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	// Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
	// that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
	// The userdev artifact is a special name and will get all sorts of transformations applied to it.
	minecraft "net.minecraftforge:forge:1.15.2-31.0.1"

	compile group: "com.github.TheRandomLabs", name: "TRLUtils-Forge", version: "1.15-SNAPSHOT", changing: true
	nonModDep group: "com.github.TheRandomLabs", name: "TRLUtils-Forge", version: "1.15-SNAPSHOT", changing: true
}

task extractNonMod(type: Copy) {
	project.trlutilsClassesDir.delete()
	project.trlutilsClassesDir.mkdirs()
	from zipTree(project.configurations.nonModDep.find {
		it.name.startsWith("TRLUtils")
	})
	into project.trlutilsClassesDir
}

compileJava.dependsOn(extractNonMod)

shadowJar {
	classifier = ""

	dependencies {
		include(dependency("com.github.TheRandomLabs:TRLUtils-Config:master-SNAPSHOT"))
		include(dependency("com.github.TheRandomLabs:TRLUtils-Forge:1.15-SNAPSHOT"))
	}

	relocate "com.therandomlabs.utils", "com.therandomlabs.randompatches.repackage.com.therandomlabs.utils"
}

reobf {
	shadowJar {
		dependsOn createMcpToSrg
		mappings = createMcpToSrg.output
	}
}

artifacts {
	archives shadowJar
}

jar {
	manifest {
		attributes([
			"FMLCorePlugin": "com.therandomlabs.randompatches.core.RPCore",
			"Specification-Title": project.name,
			"Specification-Vendor": "TheRandomLabs",
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor": "TheRandomLabs",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"Fingerprint": project.findProperty("signSHA1") ?: ""
		])
	}
}

import net.minecraftforge.gradle.common.task.SignJar

task signJar(type: SignJar, dependsOn: jar) {
	onlyIf {
		project.hasProperty("keyStore")
	}

	keyStore = project.findProperty("keyStore")
	alias = project.findProperty("keyStoreAlias")
	storePass = project.findProperty("keyStorePass")
	keyPass = project.findProperty("keyStoreKeyPass")
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

build.dependsOn signJar
