import com.modrinth.minotaur.TaskModrinthUpload

plugins {
	id "java-library"
	id "fabric-loom" version "0.5-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "1.1.0"
}

ext {
	commonGradleBranch = "master"
	minecraftVersion = "1.16.5"
	yarnMappings = "1.16.5+build.1"
	loaderVersion = "0.11.1"
	fabricAPIVersion = "0.29.3+1.16"
	modMenuVersion = "1.14.13+build.19"
	clothConfigVersion = "4.8.3"
	autoConfigVersion = "3.3.1"
	autoConfigTOMLVersion = "autoconfig-3.x.x-fabric-SNAPSHOT"
}

version = "2.1.9-fabric"
group = "com.therandomlabs.randompatches"
archivesBaseName = "randompatches"

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/${project.commonGradleBranch}/fabric.gradle"

repositories {
	maven {
		name "CottonMC"
		url "https://server.bbkr.space/artifactory/libs-release"
	}

	maven {
		url "https://maven.siphalor.de/"
	}
}

dependencies {
	modImplementation "io.github.cottonmc:cotton-client-commands:1.0.1+1.16-rc1"
	modImplementation "de.siphalor:amecsapi-1.16:1.1.1+mc1.16-rc1"
}

if (project.hasProperty("curseForgeAPIKey")) {
	curseforge {
		apiKey = project.curseForgeAPIKey

		project {
			id = "396245"

			addGameVersion "Fabric"
			addGameVersion "Java 10"
			addGameVersion "Java 9"
			addGameVersion "Java 8"
			addGameVersion "1.16.5"
			addGameVersion "1.16.4"
			addGameVersion "1.16.3"
			addGameVersion "1.16.2"

			mainArtifact(remapJar) {
				changelogType = "markdown"
				changelog = file("changelog.md")
				displayName = "RandomPatches ${version}"
				releaseType = "beta"

				relations {
					optionalDependency "amecs"
					optionalDependency "cotton-client-commands"
				}
			}

			addArtifact(sourcesJar) {
				displayName = "RandomPatches ${version} Sources"
				releaseType = "release"
			}

			addArtifact(javadocJar) {
				displayName = "RandomPatches ${version} Javadoc"
				releaseType = "release"
			}
		}

		options {
			forgeGradleIntegration = false
		}
	}

	afterEvaluate {
		tasks.curseforge396245.dependsOn build
	}
}

if (project.hasProperty("modrinthToken")) {
	task publishModrinth(type: TaskModrinthUpload) {
		token = project.modrinthToken
		projectId = "JmtW1Cr5"
		versionNumber = version
		versionName = "RandomPatches ${version}"
		changelog = new File("changelog.md").getText()
		uploadFile = remapJar
		releaseType = "beta"
		addGameVersion("1.16.5")
		addGameVersion("1.16.4")
		addGameVersion("1.16.3")
		addGameVersion("1.16.2")
		addLoader("fabric")
		addFile(sourcesJar)
		addFile(javadocJar)
	}
}
