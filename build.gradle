plugins {
	id "fabric-loom" version "0.5-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "1.1.0"
}

ext {
	commonGradleBranch = "master"
	yarnMappings = "1.16.4+build.7"
	loaderVersion = "0.10.8"
	fabricAPIVersion = "0.28.1+1.16"
	modMenuVersion = "1.14.13+build.19"
	clothConfigVersion = "4.8.3"
	autoConfigVersion = "3.3.1"
	autoConfigTOMLVersion = "autoconfig-3.x.x-fabric-SNAPSHOT"
	cottonClientCommandsVersion = "1.0.1+1.16-rc1"
}

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/${project.commonGradleBranch}/fabric/1.16.4.gradle"

version = "2.1.0"
group = "com.therandomlabs.randompatches"
archivesBaseName = "randompatches"

repositories {
	maven {
		url "https://maven.siphalor.de/"
	}
}

dependencies {
	modImplementation "de.siphalor:amecsapi-1.16:1+"
}

if (project.hasProperty("curseForgeAPIKey")) {
	curseforge {
		apiKey = project.curseForgeAPIKey

		project {
			id = "396245"

			addGameVersion "1.16.1"
			addGameVersion "1.16.2"
			addGameVersion "1.16.3"
			addGameVersion "1.16.4"

			mainArtifact(remapJar) {
				changelogType = "markdown"
				changelog = file("changelog.md")
				displayName = "RandomPatches ${version}"
				releaseType = "beta"

				relations {
					optionalDependency "cotton-client-commands"
				}
			}

			addArtifact(remapSourcesJar) {
				displayName = "RandomPatches ${version} Sources"
				releaseType = "release"
			}

			addArtifact(javadocJar) {
				displayName = "RandomPatches ${version} Javadoc"
				releaseType = "release"
			}
		}

		options {
			forgeGradleIntegration = false
		}
	}

	afterEvaluate {
		tasks.curseforge396245.dependsOn remapJar
	}
}
